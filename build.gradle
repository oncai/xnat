/*
 * web: build.gradle
 * XNAT http://www.xnat.org
 * Copyright (c) 2005-2017, Washington University School of Medicine and Howard Hughes Medical Institute
 * All Rights Reserved
 *  
 * Released under the Simplified BSD.
 */

buildscript {
    ext {
        vXnat = "1.7.5.6"
    }
    repositories {
        mavenLocal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
        jcenter()
        maven {
            url "https://nrgxnat.jfrog.io/nrgxnat/libs-release"
            name "XNAT Release Repository"
        }
        maven {
            url "https://nrgxnat.jfrog.io/nrgxnat/libs-snapshot"
            name "XNAT Snapshot Repository"
        }
    }
    dependencies {
        gradleApi()
        classpath "com.bmuschko:gradle-cargo-plugin:2.3"
        classpath "com.bmuschko:gradle-tomcat-plugin:2.4.2"
        classpath "gradle.plugin.com.palantir.gradle.gitversion:gradle-git-version:0.12.0-rc2"
        classpath "io.franzbecker:gradle-lombok:3.1.0"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.8.RELEASE"
    }
}

group "org.nrg.xnat"
version = vXnat

apply plugin: "com.bmuschko.cargo"
apply plugin: "com.bmuschko.tomcat"
apply plugin: "com.palantir.git-version"
apply plugin: "eclipse"
apply plugin: "groovy"
apply plugin: "idea"
apply plugin: "io.franzbecker.gradle-lombok"
apply plugin: "io.spring.dependency-management"
apply plugin: "jacoco"
apply plugin: "java"
apply plugin: "maven"
apply plugin: "maven-publish"
apply plugin: "war"

repositories {
    mavenLocal()
    maven {
        url "https://www.dcm4che.org/maven2/"
        name "dcm4che Maven Repository"
    }
    maven {
        url "http://maven.imagej.net/content/groups/public"
        name "ImageJ Maven Repository"
    }
    jcenter()
    mavenCentral()
    maven {
        url "https://nrgxnat.jfrog.io/nrgxnat/libs-release"
        name "XNAT Release Repository"
    }
    maven {
        url "https://nrgxnat.jfrog.io/nrgxnat/libs-snapshot"
        name "XNAT Snapshot Repository"
    }
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

def javaVersion = JavaVersion.current()
if (javaVersion.java8Compatible || javaVersion.java9Compatible || javaVersion.java10Compatible) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption("Xdoclint:none", "-quiet")
        }
    }

    if (hasProperty("rt.17.jar")) {
        // Solution for bootstrap classpath warning and possible issues with compatibility with 1.7 libraries
        // was taken from this post on discuss.gradle.org: http://bit.ly/24xD9j0
        def rt17jar = property "rt.17.jar"
        logger.info "Using ${rt17jar} as the bootstrap class path jar."
        gradle.projectsEvaluated {
            tasks.withType(JavaCompile) {
                options.fork = true
                options.compilerArgs << "-XDignore.symbol.file"
                options.bootstrapClasspath = files(rt17jar as String)
            }
        }
    } else {
        logger.warn "No value was set for the rt.17.jar build property, but you are using a Java 8- or 9-compatible JDK. You should consider setting rt.17.jar to indicate a jar file containing the Java 1.7 run-time library:\n"
        logger.warn "\n"
        logger.warn "  ./gradlew -Prt.17.jar=rt-1.7.0_45.jar war\n"
        logger.warn "\n"
        logger.warn "You can also set this property in a gradle.properties file, either in the top level of your build folder or in the folder ~/.gradle/:\n"
        logger.warn "\n"
        logger.warn "  rt.17.jar=/home/developer/.gradle/rt.17.jar\n"
        logger.warn "\n"
        logger.warn "In some isolated instances, using a bootstrap library from a JDK version later than 1.7 can result in run-time errors.\n"
    }
} else if (!javaVersion.java7Compatible) {
    throw new BuildCancelledException("You are using a JDK version (${javaVersion}) that is not compatible with Java 7. The XNAT build will fail. Please install a JDK version of Java 7 or later.")
}

if (hasProperty("archiveName")) {
    war.archiveName = archiveName.endsWith(".war") ? archiveName : "${archiveName}.war"
} else {
    war.baseName = "xnat-web"
}

configurations {
    javadocCoverage

    all {
        exclude group: "ant", module: "ant"
        exclude group: "avalon-logkit"
        exclude group: "berkeleydb"
        exclude group: "com.github.jai-imageio"
        exclude group: "com.google.code.findbugs", module: "annotations"
        exclude group: "com.metaparadigm"
        exclude group: "com.sun.jdmk", module: "jmxtools"
        exclude group: "com.sun.jmx", module: "jmxri"
        exclude group: "commons-beanutils", module: "commons-beanutils-core"
        exclude group: "commons-betwixt"
        exclude group: "commons-dbcp"
        exclude group: "commons-email"
        exclude group: "commons-messenger"
        exclude group: "commons-pool"
        exclude group: "edu.ucar", module: "netcdf"
        exclude group: "excalibur-component"
        exclude group: "excalibur-instrument"
        exclude group: "excalibur-logger"
        exclude group: "excalibur-pool"
        exclude group: "fulcrum"
        exclude group: "geronimo-spec"
        exclude group: "hsqldb"
        exclude group: "imagej"
        exclude group: "jakarta-regexp"
        exclude group: "jamon"
        exclude group: "javax.jms", module: "jms"
        exclude group: "javax.mail", module: "mail"
        exclude group: "javax.servlet", module: "servlet-api"
        exclude group: "javax.sql", module: "jdbc-stdext"
        exclude group: "javax.transaction", module: "jta"
        exclude group: "javax.xml", module: "jsr173"
        exclude group: "jdbc", module: "jdbc"
        exclude group: "jmock"
        exclude group: "jms", module: "jms"
        exclude group: "jndi"
        exclude group: "jython"
        exclude group: "mockobjects"
        exclude group: "mysql"
        exclude group: "net.sf.saxon", module: "Saxon-B"
        exclude group: "net.sf.saxon", module: "saxon"
        exclude group: "net.sf.saxon", module: "saxon-dom"
        exclude group: "net.sf.saxon", module: "saxon-xpath"
        exclude group: "ojb", module: "ojb"
        exclude group: "org.apache.geronimo.specs"
        exclude group: "org.apache.struts"
        exclude group: "org.nrg", module: "nrg"
        exclude group: "org.nrg", module: "nrgutil"
        exclude group: "org.nrg", module: "plexiviewer"
        exclude group: "org.nrg.xdat", module: "beans"
        exclude group: "org.nrg.xnat", module: "web"
        exclude group: "org.python"
        exclude group: "quartz"
        exclude group: "resources", module: "resources"
        exclude group: "servletapi"
        exclude group: "stax", module: "stax-api"
        exclude group: "tomcat"
        exclude group: "velocity"
        exclude group: "xerces"
        exclude group: "xml-apis"
        exclude group: "xml-resolver"
        exclude group: "xmlrpc"
        exclude module: "log4j-slf4j-impl"
        exclude module: "pipelineCNDAXNAT"
        exclude module: "slf4j-simple"
    }
}

dependencyManagement.imports {
    mavenBom "org.nrg:parent:${vXnat}"
}

dependencies {
    // NRG dependencies first.
    implementation "org.nrg.xnat:spawner"
    implementation("org.nrg.xnat:xnat-data-models") {
        transitive = false
    }
    implementation "org.nrg.xdat:core"
    implementation "org.nrg:framework"
    implementation "org.nrg:transaction"
    implementation "org.nrg:prefs"
    implementation "org.nrg:config"
    implementation "org.nrg:automation"
    implementation "org.nrg:dicomtools"
    implementation "org.nrg.dicom:dicom-edit4"
    implementation "org.nrg.dicom:dicom-edit6"
    implementation "org.nrg:mail"
    implementation "org.nrg:notify"

    implementation "org.nrg:dicom-xnat-mx"
    implementation "org.nrg:dicom-xnat-sop"
    implementation "org.nrg:dicom-xnat-util"
    implementation "org.nrg:ecat4xnat"
    implementation "org.nrg:ExtAttr"
    implementation "org.nrg:DicomImageUtils"
    implementation "org.nrg:PrearcImporter"
    implementation "org.nrg:SessionBuilders"

    implementation("org.nrg.xnat:plexiviewer") {
        transitive = false
    }

    implementation "org.nrg:pipelineBase"
    implementation "org.nrg:pipelineBuild"
    implementation "org.nrg:pipelineWorkflow"
    implementation "org.nrg:pipelineXNAT"
    implementation "org.nrg.xnat.pipeline:pipeline-client"
    implementation "org.nrg.xnat.pipeline:xnat-pipeline"
    implementation "org.nrg.xnat.pipeline:xnat-tools"

    implementation "dcm4che:dcm4che-core"
    implementation "dcm4che:dcm4che-image"
    implementation "dcm4che:dcm4che-imageio"
    implementation "dcm4che:dcm4che-imageio-rle"
    implementation "dcm4che:dcm4che-iod"
    implementation "dcm4che:dcm4che-net"

    implementation "org.springframework:spring-aop"
    implementation "org.springframework:spring-beans"
    implementation "org.springframework:spring-context-support"
    implementation "org.springframework:spring-context"
    implementation "org.springframework:spring-core"
    implementation "org.springframework:spring-jdbc"
    implementation "org.springframework:spring-jms"
    implementation "org.springframework:spring-messaging"
    implementation "org.springframework:spring-orm"
    implementation "org.springframework:spring-oxm"
    implementation "org.springframework:spring-tx"
    implementation "org.springframework:spring-web"
    implementation "org.springframework:spring-webmvc"

    implementation "io.springfox:springfox-swagger2"
    implementation "io.springfox:springfox-swagger-ui"

    implementation "org.springframework.security:spring-security-acl"
    implementation "org.springframework.security:spring-security-aspects"
    implementation "org.springframework.security:spring-security-config"
    implementation "org.springframework.security:spring-security-ldap"
    implementation "org.springframework.security:spring-security-taglibs"
    implementation "org.springframework.ldap:spring-ldap-core"
    implementation "org.springframework.security.oauth:spring-security-oauth2:2.2.1.RELEASE"
    implementation "org.springframework.security:spring-security-jwt:1.0.8.RELEASE"

    implementation "org.hibernate:hibernate-core"
    implementation "org.hibernate:hibernate-ehcache"
    implementation "org.hibernate:hibernate-validator"
    implementation "org.hibernate:hibernate-envers"
    implementation "net.sf.ehcache:ehcache-core"
    implementation "com.marvinformatics.hibernate:hibernate-native-json"

    implementation "org.aspectj:aspectjweaver"
    implementation "org.aspectj:aspectjrt"

    implementation "com.noelios.restlet:com.noelios.restlet"
    implementation "com.noelios.restlet:com.noelios.restlet.ext.servlet"
    implementation "org.restlet:org.restlet.ext.fileupload"

    implementation "com.fasterxml.jackson.core:jackson-annotations"
    implementation "com.fasterxml.jackson.core:jackson-core"
    implementation "com.fasterxml.jackson.core:jackson-databind"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-hibernate4"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-guava"
    implementation "org.json:json"
    implementation "com.thoughtworks.xstream:xstream"

    implementation "commons-beanutils:commons-beanutils"
    implementation "commons-codec:commons-codec"
    implementation "commons-collections:commons-collections"
    implementation "commons-configuration:commons-configuration"
    implementation "commons-digester:commons-digester"
    implementation "commons-discovery:commons-discovery"
    implementation "commons-fileupload:commons-fileupload"
    implementation "commons-net:commons-net"

    implementation "org.apache.commons:commons-configuration2"
    implementation "org.apache.commons:commons-math"
    implementation "org.apache.commons:commons-text"
    implementation "org.apache.httpcomponents:httpclient"
    implementation "org.apache.httpcomponents:httpcore"
    implementation "org.apache.httpcomponents:httpcore-nio"

    implementation "org.codehaus.groovy:groovy-all"

    implementation "net.sourceforge.saxon:saxon"
    implementation "xalan:xalan"

    implementation "eu.bitwalker:UserAgentUtils"
    implementation "com.twmacinta:fast-md5"
    implementation "com.h2database:h2"
    implementation "com.lowagie:itext"
    implementation "net.java.dev.msv:msv-core"
    implementation "gnu.getopt:java-getopt"
    implementation "org.quartz-scheduler:quartz"
    implementation "org.reflections:reflections"
    implementation "org.apache.xmlbeans:xmlbeans"
    implementation "org.apache.xmlbeans:xmlbeans-xpath"
    implementation "org.apache.xbean:xbean-spring"
    implementation "net.java.dev.msv:xsdlib"

    implementation "org.postgresql:postgresql"

    implementation "org.slf4j:slf4j-api"
    implementation "org.slf4j:slf4j-log4j12"
    implementation "org.slf4j:jul-to-slf4j"
    implementation "log4j:log4j"
    implementation "javax.servlet:jstl"
    implementation "com.google.code.gson:gson"
    implementation "com.google.guava:guava"

    compileOnly "com.google.code.findbugs:jsr305"

    runtimeOnly "org.nrg.xnatx.plugins:ximgview-plugin"
    runtimeOnly "org.apache.activemq:activemq-core"
    runtimeOnly "org.apache.ant:ant"
    runtimeOnly "axis:axis-ant"
    runtimeOnly "axis:axis-schema"
    runtimeOnly "cglib:cglib"
    runtimeOnly "org.hsqldb:hsqldb"
    runtimeOnly "org.codehaus.groovy.modules.http-builder:http-builder"
    runtimeOnly "net.imagej:ij"
    runtimeOnly "com.sun.media:jai_imageio"
    runtimeOnly "net.bull.javamelody:javamelody-core"
    runtimeOnly "org.javassist:javassist"

    providedCompile "javax.servlet:javax.servlet-api"

    tomcat "org.apache.tomcat.embed:tomcat-embed-core",
            "org.apache.tomcat.embed:tomcat-embed-logging-juli",
            "org.apache.tomcat.embed:tomcat-embed-jasper"

    cargo "org.codehaus.cargo:cargo-core-uberjar",
            "org.codehaus.cargo:cargo-ant"

    testCompile "org.nrg:test"
    testCompile "junit:junit"
    testCompile "org.springframework:spring-test"
    testCompile "org.assertj:assertj-core"

    javadocCoverage "com.manoelcampos:javadoc-coverage"
}

lombok {
    version = dependencyManagement.importedProperties["lombok.version"] as String
    sha256 = dependencyManagement.importedProperties["lombok.checksum"] as String
}

tomcatRun.contextPath = "/"
tomcatRunWar.contextPath = "/"

[tomcatRun, tomcatRunWar].each { task ->
    // TODO: This can be configured with an external folder reference to be used for loading XNAT modules.
    task.additionalRuntimeResources << file("/Users/rherrick/xnat/config/plugins")
}

task deployToTomcat(type: Copy, dependsOn: war) {
    from war.archivePath
    into "${propertyWithDefault("tomcatHome", ".")}/webapps"
}

cargo {
    // These properties must be set in the ~/.gradle/gradle.properties file or passed on the Gradle command line in
    // the form -PdeployPort=8080 -PdeployContext=/ -PdeployHost=localhost -PdeployUser=deploy -PdeployPassword=deploy
    containerId = "tomcat7x"
    port = propertyWithDefault("deployPort", 8080) as int

    deployable {
        context = propertyWithDefault("deployContext", "/")
    }

    remote {
        hostname = propertyWithDefault("deployHost", "localhost")
        username = propertyWithDefault("deployUser", "deploy")
        password = propertyWithDefault("deployPassword", "deploy")
    }
}

// Pulls in the Jenkins BUILD_NUMBER environment variable if available.
def buildDate = new Date()
def buildNumber = System.getenv().BUILD_NUMBER?.toInteger() ?: "Manual"
def isDirty, branchName, gitHash, gitHashFull, commitDistance, lastTag, isCleanTag

try {
    def gitDetails = versionDetails()
    isDirty = gitVersion().endsWith ".dirty"
    branchName = gitDetails.branchName ?: "Unknown"
    gitHash = gitDetails.gitHash
    gitHashFull = gitDetails.gitHashFull
    commitDistance = gitDetails.commitDistance
    lastTag = gitDetails.lastTag
    isCleanTag = gitDetails.isCleanTag
} catch (IllegalArgumentException e) {
    logger.info "Got an error trying to read VCS metadata from git. It's possible this project is not under VCS control. Using placeholder values for manifest entries."
    isDirty = true
    branchName = "Unknown"
    gitHash = "None"
    gitHashFull = "None"
    commitDistance = 0
    lastTag = "None"
    isCleanTag = false
}

logger.info "Build-Date:              ${buildDate}"
logger.info "Build-Number:            ${buildNumber}"
logger.info "Implementation-Version:  ${version}"
logger.info "Implementation-Sha-Full: ${gitHashFull}"
logger.info "Implementation-Sha:      ${gitHash}"
logger.info "Implementation-Commit:   ${commitDistance}"
logger.info "Implementation-LastTag:  ${lastTag}"
logger.info "Implementation-Branch:   ${branchName}"
logger.info "Implementation-CleanTag: ${isCleanTag}"
logger.info "Implementation-Dirty:    ${isDirty}"

jar {
    manifest {
        attributes "Application-Name": "XNAT",
                "Build-Date": buildDate,
                "Build-Number": buildNumber,
                "Implementation-Version": version,
                "Implementation-Sha": gitHash,
                "Implementation-Sha-Full": gitHashFull,
                "Implementation-Commit": commitDistance,
                "Implementation-LastTag": lastTag,
                "Implementation-Branch": branchName,
                "Implementation-CleanTag": isCleanTag,
                "Implementation-Dirty": isDirty
    }
}

war {
    duplicatesStrategy = DuplicatesStrategy.WARN
    manifest {
        attributes "Application-Name": "XNAT",
                "Build-Date": buildDate,
                "Build-Number": buildNumber,
                "Implementation-Version": version,
                "Implementation-Sha": gitHash,
                "Implementation-Sha-Full": gitHashFull,
                "Implementation-Commit": commitDistance,
                "Implementation-LastTag": lastTag,
                "Implementation-Branch": branchName,
                "Implementation-CleanTag": isCleanTag,
                "Implementation-Dirty": isDirty
    }
}

jacoco {
    toolVersion = dependencyManagement.importedProperties["jacoco.version"] as String
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled = false
        csv.enabled = false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

task javadocCoverageReport(type: Javadoc, dependsOn: javadoc) {
    source = sourceSets.main.allJava
    destinationDir = reporting.file("javadoc")
    classpath = configurations.compileClasspath
    options.docletpath = configurations.javadocCoverage.files.asType(List)
    options.doclet = "com.manoelcampos.javadoc.coverage.CoverageDoclet"
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) { // TODO: Issues with both coverage report and Swagger JSON generation, removing until we spend time on them and they're useful. , dependsOn: [javadocCoverageReport, swagger]) {
    classifier "javadoc"
    from javadoc.destinationDir
}

publishing.publications {
    mavenJava(MavenPublication) {
        from components.java

        artifacts {
            artifact sourceJar
            artifact javadocJar
        }

        pom.withXml {
            def root = asNode()
            root.appendNode("name", "XNAT Server")
            root.appendNode("description", "XNAT is an open-source imaging informatics software platform dedicated to helping you perform imaging-based research. XNATâ€™s core functions manage importing, archiving, processing and securely distributing imaging and related study data. But its extended uses continue to evolve.")
            root.appendNode("url", "https://bitbucket.org/xnatdev/xnat-web")
            root.appendNode("inceptionYear", "2016")

            def scm = root.appendNode("scm")
            scm.appendNode("url", "https://bitbucket.org/xnatdev/xnat-web")
            scm.appendNode("connection", "scm:https://bitbucket.org/xnatdev/xnat-web.git")
            scm.appendNode("developerConnection", "scm:git://bitbucket.org/xnatdev/xnat-web.git")

            def license = root.appendNode("licenses").appendNode("license")
            license.appendNode("name", "Simplified BSD 2-Clause License")
            license.appendNode("url", "http://xnat.org/about/license.php")
            license.appendNode("distribution", "repo")

            def developers = root.appendNode("developers")
            def rherrick = developers.appendNode("developer")
            rherrick.appendNode("id", "rherrick")
            rherrick.appendNode("name", "Rick Herrick")
            rherrick.appendNode("email", "jrherrick@wustl.edu")
        }
    }
}
publishing.repositories {
    maven {
        credentials {
            // These properties must be set in the ~/.gradle/gradle.properties file or passed on the Gradle command
            // line in the form -PrepoUsername=foo -PrepoPassword=bar.
            username propertyWithDefault("repoUsername", "username")
            password propertyWithDefault("repoPassword", "password")
        }
        url project.version.endsWith("-SNAPSHOT") ? "https://nrgxnat.jfrog.io/nrgxnat/libs-snapshot-local" : "https://nrgxnat.jfrog.io/nrgxnat/libs-release-local"
    }
}

def propertyWithDefault(String name, Object value) {
    hasProperty(name) ? property(name) : value
}
